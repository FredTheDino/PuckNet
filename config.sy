use game
use menu

BROWSER_SERVER_IP :: "fuffens.xyz"
BROWSER_SERVER_PORT :: 8388

SERVER_PORT :: 8588

VOLUME := 5
MAX_VOLUME :: 10
VOLUME_FLOAT := as_float(VOLUME) / as_float(MAX_VOLUME)

SHOWED_TUTORIAL := false
SHOW_TUTORIAL := false

set_SHOWED_TUTORIAL :: fn showed: bool {
    SHOWED_TUTORIAL = showed
}

set_SHOW_TUTORIAL :: fn show: bool {
    SHOW_TUTORIAL = show
}

new_settings_state :: fn -> game.State {
    self : !game.State = nil

    settings_menu := menu.menu_new' [
        "", // Volume
    ], "Settings"

    self = game.State {
        name: "PuckNET Settings",
        update: fn delta: float {
            settings_menu.update' delta

            volume_s := "Volume: "
            i := 0
            loop i < VOLUME {
                volume_s += "|"
                i += 1
            }
            loop i < MAX_VOLUME {
                volume_s += "-"
                i += 1
            }
            settings_menu.lines[0] = volume_s
        },
        draw: fn {
            settings_menu.draw'
        },
        process_input: fn {
            input := menu.from_input' game.poll_input'

            if settings_menu.process_input(input) {
                // Handle left/right if volume is focused
                if settings_menu.line_index == 0 {
                    if input.left {
                        VOLUME += 1
                    } else if input.right {
                        VOLUME -= 1
                    }
                    VOLUME = clamp' VOLUME, 0, MAX_VOLUME
                    VOLUME_FLOAT = as_float(VOLUME) / as_float(MAX_VOLUME)
                }
            }
        },
        cleanup: fn {},
    }
    ret self
}
