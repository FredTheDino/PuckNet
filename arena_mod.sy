use assets
use collision
use game

Goal :: blob {
    rad: float,
    pos: (float, float),
    vel: (float, float),
    cage: collision.CircleBody,
    draw: fn -> void,
    update: fn float -> void,
}

:Goal is :game.Entity
:Goal is :collision.Circle

goal_new :: fn pos: (float, float), dir: float, arena_scale: float -> Goal {

    scale :: 13. / 100. * arena_scale
    px :: 1. / 13. * scale
    width :: 8. * px
    height :: 13. * px
    offset :: (arena_scale * .89 - width / 2., 0.) * dir

    self: !Goal = nil
    self = Goal{
        rad: width / 2. * .75,
        pos: pos + offset + (px, 0.) * dir,
        vel: (0.0, 0.5),
        cage: collision.CircleBody{
            mass: 0.,
            rad: width - px,
            pos: pos - (offset + (width / 2. - px, 0.) * dir),
            vel: (0.0, 0.5),
        },
        draw: fn {
            l_gfx_sprite' assets.goal, (0, 0), self.pos, (width * dir, height)
        },
        update: fn delta: float {
            self.pos += self.vel * delta
            self.cage.pos += self.cage.vel * delta
            if abs(self.pos[1]) > arena_scale * .7 / 2. {
                self.vel = (self.vel[0], -sign(self.pos[1]) * abs(self.vel[1]))
                self.cage.vel = self.vel
            }
        }
    }

    ret self
}

Arena :: blob {
    draw: fn -> void,
    update: fn float -> void,
    walls: [collision.Wall],
    corners: [collision.Circle],
    goals: [Goal],
}

Mupp :: blob {
    pos: (float, float),
    bobbiness: float,
    rgb: (float, float, float),
    sprite: int,
}

:Arena is :game.Entity

arena_new :: fn pos: (float, float) -> Arena {
    pos :: (0.0, 0.0)
    scale :: 2.0

    r :: scale / 5.0
    x :: 0.80 * scale
    y :: 0.30 * scale

    self: !Arena = nil
    self = Arena{
        draw: fn -> void {
            gray :: (0.2, 0.2, 0.2)
            l_gfx_rect((0.0, 0.0), (20.0, 20.0), gray)

            t :: rem( 0.1 * l_time(), 4.0 * scale)

            l_gfx_sprite(assets.ads, (0, 0), pos + (t, (scale - 0.2) / 1.5), (4.0, 4.0 * 20.0 / 401.0) * scale)
            l_gfx_sprite(assets.ads, (0, 0), pos + (t - 4.0 * scale, (scale - 0.2) / 1.5), (4.0, 4.0 * 20.0 / 401.0) * scale)

            l_gfx_sprite(assets.ads, (0, 0), pos + (t - 4.0 * scale, (scale - 0.2) / 1.5), (4.0, 4.0 * 20.0 / 401.0) * scale)

            l_gfx_rect(-scale * 1.3, 0.0, scale, 3.0, gray)
            l_gfx_rect(scale * 1.3, 0.0, scale, 3.0, gray)

            l_gfx_sprite(assets.arena, (0, 0), pos, (2.0, 1.0) * scale)
            l_gfx_sprite(assets.border, (0, 0), pos + (0.0, scale / 2.0), (2.0, 2.0 * 40.0 / 200.0) * scale)
            // TODO(ed): Move this to a custom file - it needs to be
            // rendered on top of the player
            self.goals[0].draw'
            self.goals[1].draw'
        },
        update: fn delta: float {
            self.goals[0].update' delta
            self.goals[1].update' delta
        },
        walls: [
            collision.Wall{ normal: (0.0, -1.0), pos: (0.0,  scale / 2.0) },
            collision.Wall{ normal: (0.0,  1.0), pos: (0.0, -scale / 2.0) },
            collision.Wall{ normal: (-1.0, 0.0), pos: ( scale,       0.0) },
            collision.Wall{ normal: ( 1.0, 0.0), pos: (-scale,       0.0) },
        ],
        corners: [
            // Radius is negative because of collisions inside of the circle
            collision.Circle { rad: -r, pos: (-x,  y) },
            collision.Circle { rad: -r, pos: (-x, -y) },
            collision.Circle { rad: -r, pos: ( x, -y) },
            collision.Circle { rad: -r, pos: ( x,  y) },
        ],
        goals: [
            goal_new(pos, 1., scale),
            goal_new(pos, -1., scale),
        ],
    }

    ret self
}
